buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
        classpath 'com.avast.gradle:docker-compose-gradle-plugin:0.3.20'
    }
}

plugins {
    id 'net.researchgate.release' version '2.4.0'
}

group 'cz.alenkacz.db'
version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: "maven-publish"
apply plugin: 'com.jfrog.bintray'
apply plugin: 'docker-compose'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    scalaVersion = '2.12'
    scalaLibraryVersion = '2.12.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

dependencies {
    compile 'com.typesafe:config:1.3.1'
    compile "org.scala-lang:scala-library:${scalaLibraryVersion}"
    compile 'com.github.mauricio:postgresql-async_2.12:0.2.21'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.scalatest:scalatest_${scalaVersion}:3.0.1"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'postgres-scala_2.12'
        desc = 'Asynchronous postgresql access from Scala.'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/alenkacz/postgres-scala.git'
        websiteUrl = 'https://github.com/alenkacz/postgres-scala'
        issueTrackerUrl = 'https://github.com/alenkacz/postgres-scala/issues'
        labels = ['postgresql', 'scala', 'database']
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}

dockerCompose.isRequiredBy(test)
test.doFirst {
    dockerCompose.exposeAsEnvironment(test)
}